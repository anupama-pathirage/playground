configurations {
    api {
        transitive = false
    }
}

def env = System.getenv()
def gcpProjectID = env["BPG_GCP_PROJECT_ID"]
def imageTag = 'gcr.io/' + gcpProjectID +'/playground-gists-api:' + project.version

dependencies {
    api project(path: ":api:controller", configuration: "gists_api")
}

task cleanResources(type: Delete) {
    delete "api"
}

task copyApi(type: Copy) {
    dependsOn cleanResources
    dependsOn(':api:controller:build')
    from configurations.api
    into "api" 
}

def inputFiles = [
    project.file("cloudbuild.yaml"),
    project.file("Dockerfile")
]

task cloudBuild(type: Exec) {
    dependsOn copyApi
    inputs.files(inputFiles)
    commandLine "gcloud", "builds", "submit", "--config", "cloudbuild.yaml", 
            '--substitutions=_IMAGE_TAG=' + imageTag, "."
}

task build(type: Exec) {
    dependsOn copyApi
    inputs.files(inputFiles)
    commandLine 'docker', 'build', '-t', imageTag, '.'
}

def containerIdFile = new File(projectDir, "containerId.txt");
if (!containerIdFile.exists()) {
    containerIdFile.write("");
}

task start(type: Exec) {
    dependsOn build
    commandLine "docker", "run",
        "--env", "REDIS_WRITE_HOST=docker.for.mac.host.internal",
        "--env", "REDIS_READ_HOST=docker.for.mac.host.internal",
        "-d", "-p", "9093:9090", imageTag
    standardOutput = new ByteArrayOutputStream()
	doLast {
        containerIdFile.text = standardOutput.toString()
        println standardOutput.toString()
	}
}

task logs(type: Exec) {
    commandLine "docker", "logs", "-f", containerIdFile.text.trim()
}

task stop(type: Exec) {
    commandLine "docker", "kill", containerIdFile.text.trim()
    doLast {
        containerIdFile.text = ""
	}
}
